rem
rem New message script for FARMail.
rem

rem Path to the scripts.ini file
ini_file = "scripts.ini"

//if you like a menu of options instead of addressbook set to 1
select = getini("global_settings","show_menu",0,ini_file)

templ_count = getini("templates","count",0,ini_file)
templ="global_template"

file_with_song = getini("global_settings","file_with_song","",ini_file)
file_with_song_pos = getini("global_settings","file_with_song_pos",0,ini_file)
origins_file = getini("global_settings","origins_file","",ini_file)

rem add X-Comment-To field (1 = on , 0 = off)
use_x_comment = getini("global_settings","use_x_comment",0,ini_file)

rem add Organization field (1 = on , 0 = off)
use_organization = getini("global_settings","use_organization",0,ini_file)

rem Signature start marker, every thing that comes after this is thrown away
rem (Should not be an empty string)
sig_marker = getini("global_settings","signature_marker","-- ",ini_file)

rem Show Notice-Requested-Upon-Delivery-To headers
show_dilivery_notice_headers = getini("global_settings","show_dilivery_notice_headers",0,ini_file)

rem Message-ID
new_msg_id = date() + time()
new_msg_id = substr(new_msg_id,6,4) + substr(new_msg_id,3,2) + substr(new_msg_id,0,2) + substr(new_msg_id,10,2) + substr(new_msg_id,13,2) + substr(new_msg_id,16,2)
new_msg_id = sprintf("%s.%10.10d.%10.10d@farmail",new_msg_id,random(100000000),random(100000000))

rem ---------------------------- script start -------------------------------------

rem parse string strored in variable sig_line
rem %name% var is taken from the name variable
rem %fullname% var is taken from the fullname variable
rem return sig_line
sub expand_vars
  while(strstr(sig_line,"%name%")!=-1)
    spec=strstr(sig_line,"%name%")
    sig_line=substr(sig_line,0,spec)+name+substr(sig_line,spec+6)
  wend
  while(strstr(sig_line,"%fullname%")!=-1)
    spec=strstr(sig_line,"%fullname%")
    sig_line=substr(sig_line,0,spec)+fullname+substr(sig_line,spec+10)
  wend
  while(strstr(sig_line,"%date%")!=-1)
    spec=strstr(sig_line,"%date%")
    sig_line=substr(sig_line,0,spec)+date()+substr(sig_line,spec+6)
  wend
  while(strstr(sig_line,"%time%")!=-1)
    spec=strstr(sig_line,"%time%")
    sig_line=substr(sig_line,0,spec)+time()+substr(sig_line,spec+6)
  wend
  while(strstr(sig_line,"%farmail%")!=-1)
    spec=strstr(sig_line,"%farmail%")
    sig_line=substr(sig_line,0,spec)+version(0)+substr(sig_line,spec+9)
  wend
  while(strstr(sig_line,"%far%")!=-1)
    spec=strstr(sig_line,"%far%")
    sig_line=substr(sig_line,0,spec)+version(1)+substr(sig_line,spec+5)
  wend
  while(strstr(sig_line,"%windows%")!=-1)
    spec=strstr(sig_line,"%windows%")
    sig_line=substr(sig_line,0,spec)+version(2)+substr(sig_line,spec+9)
  wend
  while(strstr(sig_line,"%song%")!=-1)
    spec=strstr(sig_line,"%song%")
    sig_line=substr(sig_line,0,spec)+winampsong()+substr(sig_line,spec+6)
  wend
  while(strstr(sig_line,"%song_from_file%")!=-1)
    spec=strstr(sig_line,"%song_from_file%")
    sig_line=substr(sig_line,0,spec)+fileline(file_with_song,file_with_song_pos)+substr(sig_line,spec+16)
  wend
  while(strstr(sig_line,"%origin%")!=-1)
    spec=strstr(sig_line,"%origin%")
    max_origins = filecount(origins_file)
    sig_line=substr(sig_line,0,spec)+fileline(origins_file,random(max_origins))+substr(sig_line,spec+8)
  wend
  while(strstr(sig_line,"%stat%")!=-1)
    spec=strstr(sig_line,"%stat%")
    i_stat=winampstate()
    if (i_stat==3)
      s_stat="paused:"
    else
      if (i_stat==1)
        s_stat="playing:"
      else
        if (i_stat==0)
          s_stat="stopped:"
        else
          s_stat="not active now"
        endif
      endif
    endif
    sig_line=substr(sig_line,0,spec)+s_stat+substr(sig_line,spec+6)
  wend
endsub

rem parse one email stored in variable email and delete it from variable email
rem return extracted_email, extracted_name, extracted_fullname
rem used variables email_i1,email_i2
sub parse_email
  extracted_email=""
  extracted_name=""
  extracted_fullname=""

  email_i1=strstr(email,"<")
  if (email_i1>=0)
    email_i2=strstr(email,">")
    temp_str=trim(substr(email,0,email_i1-1))
    if (temp_str[0]==',')
      temp_str=trim(substr(temp_str,1,strlen(temp_str)))
    endif
    if (temp_str[0]=='"')
      temp_str=trim(substr(temp_str,1,strlen(temp_str)-2))
    endif
    extracted_fullname=temp_str
    temp_str=substr(email,email_i1+1,email_i2-email_i1-1)
    email=substr(email,email_i2+1,strlen(email))
  else
    email_i1=strstr(email,",")
    if (email_i1==0)
      email=substr(email,1,strlen(email))
      email_i1=strstr(email,",")
    endif
    if (email_i1>=0)
      temp_str=substr(email,0,email_i1)
      email=substr(email,email_i1+1,strlen(email))
    else
      temp_str=email
      email=""
    endif
  endif
  extracted_email=trim(temp_str)
  if(extracted_fullname=="")
    extracted_fullname=substr(extracted_email,0,strstr(extracted_email,"@"))
    extracted_name=extracted_fullname
  else
    email_i1=0
    while(extracted_fullname[email_i1]&&extracted_fullname[email_i1]!=' '&&extracted_fullname[email_i1]!=9)
      email_i1++
    wend
    extracted_name=substr(extracted_fullname,0,email_i1)
  endif
endsub

rem text to encode stored in variable text
rem encoding in encoding
rem return encoded text in text
rem used variables j, x
sub encode_if_needed
  if (encoding!="")
    j=strlen(text)
    if (j>0)
      x=0
      while (x<j)
        if (text[x]>127 || text[x]<32)
          j=-1
          break
        endif
        x++
      wend
      if (j==-1)
        text = encodeheader(text,encoding)
      endif
    endif
  endif
endsub

to_field = ""
to_name = ""
to_fullname = ""

if (select)
  select=0
  if (templ_count==9)
    select=usrmenu(getini("global_template","title","",ini_file),getini(getini("templates","templ1","",ini_file),"title","",ini_file),getini(getini("templates","templ2","",ini_file),"title","",ini_file),getini(getini("templates","templ3","",ini_file),"title","",ini_file),getini(getini("templates","templ4","",ini_file),"title","",ini_file),getini(getini("templates","templ5","",ini_file),"title","",ini_file),getini(getini("templates","templ6","",ini_file),"title","",ini_file),getini(getini("templates","templ7","",ini_file),"title","",ini_file),getini(getini("templates","templ8","",ini_file),"title","",ini_file),getini(getini("templates","templ9","",ini_file),"title","",ini_file))
  endif
  if (templ_count==8)
    select=usrmenu(getini("global_template","title","",ini_file),getini(getini("templates","templ1","",ini_file),"title","",ini_file),getini(getini("templates","templ2","",ini_file),"title","",ini_file),getini(getini("templates","templ3","",ini_file),"title","",ini_file),getini(getini("templates","templ4","",ini_file),"title","",ini_file),getini(getini("templates","templ5","",ini_file),"title","",ini_file),getini(getini("templates","templ6","",ini_file),"title","",ini_file),getini(getini("templates","templ7","",ini_file),"title","",ini_file),getini(getini("templates","templ8","",ini_file),"title","",ini_file))
  endif
  if (templ_count==7)
    select=usrmenu(getini("global_template","title","",ini_file),getini(getini("templates","templ1","",ini_file),"title","",ini_file),getini(getini("templates","templ2","",ini_file),"title","",ini_file),getini(getini("templates","templ3","",ini_file),"title","",ini_file),getini(getini("templates","templ4","",ini_file),"title","",ini_file),getini(getini("templates","templ5","",ini_file),"title","",ini_file),getini(getini("templates","templ6","",ini_file),"title","",ini_file),getini(getini("templates","templ7","",ini_file),"title","",ini_file))
  endif
  if (templ_count==6)
    select=usrmenu(getini("global_template","title","",ini_file),getini(getini("templates","templ1","",ini_file),"title","",ini_file),getini(getini("templates","templ2","",ini_file),"title","",ini_file),getini(getini("templates","templ3","",ini_file),"title","",ini_file),getini(getini("templates","templ4","",ini_file),"title","",ini_file),getini(getini("templates","templ5","",ini_file),"title","",ini_file),getini(getini("templates","templ6","",ini_file),"title","",ini_file))
  endif
  if (templ_count==5)
    select=usrmenu(getini("global_template","title","",ini_file),getini(getini("templates","templ1","",ini_file),"title","",ini_file),getini(getini("templates","templ2","",ini_file),"title","",ini_file),getini(getini("templates","templ3","",ini_file),"title","",ini_file),getini(getini("templates","templ4","",ini_file),"title","",ini_file),getini(getini("templates","templ5","",ini_file),"title","",ini_file))
  endif
  if (templ_count==4)
    select=usrmenu(getini("global_template","title","",ini_file),getini(getini("templates","templ1","",ini_file),"title","",ini_file),getini(getini("templates","templ2","",ini_file),"title","",ini_file),getini(getini("templates","templ3","",ini_file),"title","",ini_file),getini(getini("templates","templ4","",ini_file),"title","",ini_file))
  endif
  if (templ_count==3)
    select=usrmenu(getini("global_template","title","",ini_file),getini(getini("templates","templ1","",ini_file),"title","",ini_file),getini(getini("templates","templ2","",ini_file),"title","",ini_file),getini(getini("templates","templ3","",ini_file),"title","",ini_file))
  endif
  if (templ_count==2)
    select=usrmenu(getini("global_template","title","",ini_file),getini(getini("templates","templ1","",ini_file),"title","",ini_file),getini(getini("templates","templ2","",ini_file),"title","",ini_file))
  endif
  if (templ_count==1)
    select=usrmenu(getini("global_template","title","",ini_file),getini(getini("templates","templ1","",ini_file),"title","",ini_file))
  endif
  if(select==-1)
    exit()
  endif
  if(select==0)
    select = usrmenu("&Get recipient from AddressBook","&Enter recipient")
    if(select == 0)
      to_field = addressbook()
    endif
    if(select == 1)
      to_field = usrinput("Enter recipient","Enter recipient's address")
    endif
    if(strlen(to_field))
      email=to_field
      gosub parse_email
    else
      to_field=""
      extracted_name=""
      extracted_fullname=""
    endif
  else
    templ=getini("templates","templ"+string(select),"",ini_file)
    to_field=getini(templ,"to_out","",ini_file)
    if(to_field=="")
      to_field=getini(templ,"to","",ini_file)
    endif
    extracted_name=""
    extracted_fullname=""
  endif
  to_name=extracted_name
  to_fullname=extracted_fullname
else
  select = usrmenu("&Get recipient from AddressBook","&Enter recipient")

  if(select == 0)
    to_field = addressbook()
  endif
  if(select == 1)
    to_field = usrinput("Enter recipient","Enter recipient's address")
  endif

  email = to_field
  gosub parse_email
  to_name=extracted_name
  to_fullname=extracted_fullname
  to_email=extracted_email

  if(to_email!="")
    i=1
    while(i<=templ_count)
      templ=getini("templates","templ"+string(i),"",ini_file)
      email=getini(templ,"to","",ini_file)
      gosub parse_email
      if(extracted_email==to_email)
        email2=getini(templ,"to_out","",ini_file)
        if(email2!="")
          to_field=email2
        endif
        to_name=""
        to_fullname=""
        break
      endif
      i++
      templ="global_template"
    wend
  endif
endif

subject_field = usrinput("Enter subject","Enter message subject")

encoding = getini(templ,"encoding","",ini_file)

email=to_field
gosub parse_email
to_name=extracted_name
to_fullname=extracted_fullname

to_field = ""
while(1)
  text=extracted_fullname
  if (text!="")
    gosub encode_if_needed
    to_field = to_field+"\"" + text + "\" " + "<"+extracted_email+">"
  else
    to_field = to_field+"<"+extracted_email+">"
  endif
  if(email=="")
    break
  else
    gosub parse_email
    to_field=to_field+","
  endif
wend

text=subject_field
gosub encode_if_needed
subject_field=text

i = 0
insline(i,0)
setline( "%start%", i++ )
insline(i,0)
from=getini(templ,"from","",ini_file)
if (from!="")
  setline( " set %from% = " + from, i++ )
  insline(i,0)
endif
setline( " set %to% = " + to_field, i++ )
insline(i,0)
reply_to=getini(templ,"reply_to","",ini_file)
if (reply_to!="")
  setline( " set %reply% = " + reply_to, i++ )
else
  setline( " ;set %reply% = ", i++ )
endif
insline(i,0)
setline( " set %subj% = " + subject_field, i++ )
insline(i,0)
setline( " ;set %cc% = ", i++ )
insline(i,0)
setline( " ;set %bcc% = ", i++ )
insline(i,0)
if(use_organization)
  text=getini(templ,"organization","",ini_file)
  gosub encode_if_needed
  setline( " set %org% = " + text, i++ )
  insline(i,0)
endif
setline( " header Message-ID = <" + new_msg_id + ">", i++ )
insline(i,0)
fidonewsgroup=getini(templ,"fidonewsgroup","",ini_file)
if(fidonewsgroup!="")
  use_x_comment=1
  setline(" header Newsgroups = " + fidonewsgroup,i++)
  insline(i,0)
  fidokeyword=getini(templ,"fidokeyword","",ini_file)
  if(fidokeyword!="")
    setline(" header Keywords = " + fidokeyword,i++)
    insline(i,0)
  endif
endif
if(use_x_comment)
  text=to_fullname
  gosub encode_if_needed
  setline(" header X-Comment-To = " + text,i++)
  insline(i,0)
endif
if (show_dilivery_notice_headers)
  setline( " ;header Notice-Requested-Upon-Delivery-To = %from%", i++ )
  insline(i,0)
  setline( " ;header Disposition-Notification-To = %from%", i++ )
  insline(i,0)
  setline( " ;header Return-Receipt-To = %from%", i++ )
  insline(i,0)
  setline( " ;header X-Confirm-Reading-To = %from%", i++ )
  insline(i,0)
  setline( " ;header Read-Receipt-To = %from%", i++ )
  insline(i,0)
endif
setline( " ;attach ", i++ )
insline(i,0)
setline( "%end%", i++ )
insline(i,0)
x = i
greet_new_count=getini(templ,"greet_new_count",0,ini_file)
j=1
name=to_name
fullname=to_fullname
while(j<=greet_new_count)
  sig_line=getini(templ,"greet_new"+string(j),"",ini_file)
  gosub expand_vars
  setline(sig_line,i++)
  insline(i,0)
  j++
wend

i = nlines()-1
insline(i,0)
insline(i++,0)
insline(i++,0)
setline(sig_marker, i )

pos=i
sign_count=getini(templ,"sign_count",0,ini_file)
i=1
while(i<=sign_count)
  sig_line=getini(templ,"sign"+string(i),"",ini_file)
  gosub expand_vars
  insline(++pos,1)
  setline(sig_line,pos)
  i++
wend

rem Set cursor after the greeting message
if((strlen(to_name) > 0) || (strlen(to_fullname) > 0) || (templ!="global_template"))
  setpos(x+2, 0)
else
  setpos(x, 0)
endif
